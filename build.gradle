import java.time.LocalDate

plugins {
	id "java"
	id "idea"
	id "application"
	id "jacoco"
	id "org.danilopianini.git-sensitive-semantic-versioning-gradle-plugin" version "7.0.8"
	id "io.freefair.lombok" version "9.1.0"
	id "com.diffplug.spotless" version "8.1.0"
	id "org.openapi.generator" version "7.18.0"
	id "io.swagger.core.v3.swagger-gradle-plugin" version "2.2.41"
	id "org.flywaydb.flyway" version "11.20.0" apply false
	id "nu.studer.jooq" version "10.2" apply false
	id "io.micronaut.application" version "4.6.1"
}

group "com.autonomouslogic.everef"

java {
	toolchain {
		sourceCompatibility = JavaVersion.VERSION_21
		targetCompatibility = JavaVersion.VERSION_21
	}
}

ext {
	mainClassName = "com.autonomouslogic.everef.Main"
	envProperties = new Properties()
	if (file("local.env").exists()) {
		file("local.env").withInputStream { envProperties.load(it) }
	}
	micronautVersion = "4.10.6"
}

repositories {
	mavenLocal()
	mavenCentral()
}

java {
	withJavadocJar()
	withSourcesJar()
}

dependencies {
	annotationProcessor "com.google.dagger:dagger-compiler:2.57.2"
	implementation project(":database")
	implementation "com.google.dagger:dagger:2.57.2"
	implementation "com.google.guava:guava:33.5.0-jre"
	implementation "org.apache.logging.log4j:log4j-api:2.25.3"
	implementation "org.apache.logging.log4j:log4j-core:2.25.3"
	implementation "org.apache.logging.log4j:log4j-layout-template-json:2.25.3"
	implementation "org.apache.logging.log4j:log4j-slf4j2-impl:2.25.3"
	implementation "io.reactivex.rxjava3:rxjava:3.1.12"
	implementation "com.autonomouslogic.commons:commons-java:1.9.8"
	implementation("de.neuland-bfi:pug4j:2.4.1") {
		exclude group: "org.graalvm.js"
		exclude group: "org.graalvm.sdk"
		exclude group: "org.graalvm.compiler"
		exclude group: "org.graalvm.tools"
		exclude group: "com.vladsch.flexmark"
	}
	implementation "software.amazon.awssdk:s3:2.41.2"
	implementation "software.amazon.awssdk:dynamodb:2.41.2"
	implementation "com.autonomouslogic.dynamomapper:dynamo-mapper:2.1.18"
	implementation "com.fasterxml.jackson.core:jackson-annotations:2.20"
	implementation "com.fasterxml.jackson.core:jackson-core:2.20.1"
	implementation "com.fasterxml.jackson.core:jackson-databind:2.20.1"
	implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.20.1"
	implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.20.1"
	implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.20.1"
	implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.20.1"
	implementation "org.apache.commons:commons-pool2:2.13.0"
	implementation "org.apache.commons:commons-compress:1.28.0"
	implementation "org.tukaani:xz:1.11"
	implementation "com.h2database:h2-mvstore:2.4.240"
	implementation "com.squareup.okhttp3:okhttp:5.3.2"
	implementation "org.apache.commons:commons-csv:1.14.1"
	implementation "net.gpedro.integrations.slack:slack-webhook:1.4.0"
	implementation "org.jsoup:jsoup:1.22.1"
	implementation "commons-codec:commons-codec:1.20.0"
	implementation "io.dropwizard.metrics:metrics-core:4.2.37"
	implementation "com.autonomouslogic.customspecfilter:custom-spec-filter:1.2.3"
	implementation "io.swagger.core.v3:swagger-core:2.2.41"
	implementation "io.swagger.core.v3:swagger-jaxrs2:2.2.41"
	implementation "javax.ws.rs:javax.ws.rs-api:2.1.1"
	implementation "javax.annotation:javax.annotation-api:1.3.2"
	implementation "org.openapitools:jackson-databind-nullable:0.2.8"
	implementation "org.apache.commons:commons-rng-simple:1.6"
	implementation "org.bouncycastle:bcprov-jdk18on:1.83"
	implementation "io.sentry:sentry:8.29.0"
	implementation "com.github.scribejava:scribejava-core:8.3.3"
	implementation "org.apache.commons:commons-lang3:3.20.0"
	implementation "com.google.code.findbugs:jsr305:3.0.2"

	// Jooq, Flyway, H2, Postgresql
	implementation "org.jooq:jooq:3.20.10"
	implementation "org.flywaydb:flyway-core:11.20.3"
	implementation "org.flywaydb:flyway-database-postgresql:11.20.0"
	implementation "com.h2database:h2:2.4.240"
	implementation "org.postgresql:postgresql:42.7.8"

	// Micronaut
	annotationProcessor "io.micronaut.jaxrs:micronaut-jaxrs-processor"
	implementation "io.micronaut.platform:micronaut-parent:$micronautVersion"
	implementation "io.micronaut:micronaut-jackson-databind"
	implementation "io.micronaut.jaxrs:micronaut-jaxrs-server"
	implementation "io.micronaut:micronaut-management"

	// Helidon
	implementation "io.helidon.webserver:helidon-webserver:4.3.3"
	implementation "io.helidon.logging:helidon-logging-slf4j:4.3.3"

	testAnnotationProcessor "com.google.dagger:dagger-compiler:2.57.2"
	testImplementation "com.google.dagger:dagger:2.57.2"
	testImplementation "org.junit.jupiter:junit-jupiter:6.0.1"
	testImplementation "org.junit.jupiter:junit-jupiter-params:6.0.1"
	testImplementation "org.junit-pioneer:junit-pioneer:2.3.0"
	testImplementation "org.mockito:mockito-junit-jupiter:5.21.0"
	testImplementation "com.squareup.okhttp3:mockwebserver:5.3.2"
}

task refDataSpec(type: io.swagger.v3.plugins.gradle.tasks.ResolveTask) {
	group = "openapi tools"
	filterClass = "com.autonomouslogic.customspecfilter.CustomSpecFilter"
	outputFormat = "YAML"
	prettyPrint = "true"
	sortOutput = true
	alwaysResolveAppPath = false
	skipResolveAppPath = false
	readAllResources = true
	encoding = "UTF-8"
	classpath = sourceSets.main.runtimeClasspath
	resourcePackages = ["com.autonomouslogic.everef.refdata"]
	outputDir = file("spec")
	outputFileName = "reference-data"

	classpath = sourceSets.main.runtimeClasspath
	buildClasspath = classpath
}

task referenceDataClientGenerate(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
	group = "openapi tools"
	generatorName = "java"
	library = "native"
	inputSpec = "$rootDir/spec/reference-data.yaml"
	outputDir = "$buildDir/generated/openapi/refdata"
	apiPackage = "com.autonomouslogic.everef.openapi.refdata.api"
	modelPackage = "com.autonomouslogic.everef.openapi.refdata.model"
	invokerPackage = "com.autonomouslogic.everef.openapi.refdata.invoker"
	configOptions = [
			serializationLibrary: "jackson",
			dateLibrary: "java8",
			asyncNative: "false"
	]
	additionalProperties = [
			supportUrlQuery: "false"
	]
	schemaMappings = [
			"Bundle": "com.autonomouslogic.everef.refdata.Bundle",
			"Blueprint": "com.autonomouslogic.everef.refdata.Blueprint",
			"BlueprintActivity": "com.autonomouslogic.everef.refdata.BlueprintActivity",
			"BlueprintMaterial": "com.autonomouslogic.everef.refdata.BlueprintMaterial",
			"Coordinate": "com.autonomouslogic.everef.refdata.Coordinate",
			"DogmaAttribute": "com.autonomouslogic.everef.refdata.DogmaAttribute",
			"DogmaEffect": "com.autonomouslogic.everef.refdata.DogmaEffect",
			"DogmaTypeAttribute": "com.autonomouslogic.everef.refdata.DogmaTypeAttribute",
			"DogmaTypeEffect": "com.autonomouslogic.everef.refdata.DogmaTypeEffect",
			"Icon": "com.autonomouslogic.everef.refdata.Icon",
			"IndustryModifierActivities": "com.autonomouslogic.everef.refdata.IndustryModifierActivities",
			"IndustryModifierBonuses": "com.autonomouslogic.everef.refdata.IndustryModifierBonuses",
			"InventoryCategory": "com.autonomouslogic.everef.refdata.InventoryCategory",
			"InventoryGroup": "com.autonomouslogic.everef.refdata.InventoryGroup",
			"InventoryType": "com.autonomouslogic.everef.refdata.InventoryType",
			"InventoryTypeTraits": "com.autonomouslogic.everef.refdata.InventoryTypeTraits",
			"MarketGroup": "com.autonomouslogic.everef.refdata.MarketGroup",
			"MetaGroup": "com.autonomouslogic.everef.refdata.MetaGroup",
			"ModifierInfo": "com.autonomouslogic.everef.refdata.ModifierInfo",
			"Mutaplasmid": "com.autonomouslogic.everef.refdata.Mutaplasmid",
			"MutaplasmidDogmaModifications": "com.autonomouslogic.everef.refdata.MutaplasmidDogmaModifications",
			"MutaplasmidTypeMapping": "com.autonomouslogic.everef.refdata.MutaplasmidTypeMapping",
			"ProducingBlueprint": "com.autonomouslogic.everef.refdata.ProducingBlueprint",
			"RefDataMeta": "com.autonomouslogic.everef.refdata.RefDataMeta",
			"RefDataMetaFileInfo": "com.autonomouslogic.everef.refdata.RefDataMetaFileInfo",
			"Region": "com.autonomouslogic.everef.refdata.Region",
			"Schematic": "com.autonomouslogic.everef.refdata.Schematic",
			"Skill": "com.autonomouslogic.everef.refdata.Skill",
			"TraitBonus": "com.autonomouslogic.everef.refdata.TraitBonus",
			"TypeMaterial": "com.autonomouslogic.everef.refdata.TypeMaterial",
			"Unit": "com.autonomouslogic.everef.refdata.Unit",
			"UsedInBlueprint": "com.autonomouslogic.everef.refdata.UsedInBlueprint"
	]
	logToStderr = true
}
sourceSets.main.java.srcDirs files("${buildDir}/generated/openapi/refdata/src/main/java")
		.builtBy(tasks.referenceDataClientGenerate)
compileJava.dependsOn(tasks.referenceDataClientGenerate)
sourcesJar.dependsOn(tasks.referenceDataClientGenerate)
delombok.dependsOn(tasks.referenceDataClientGenerate)

task esiClientGenerate(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
	group = "openapi tools"
	generatorName = "java"
	library = "native"
	inputSpec = "$rootDir/spec/esi-swagger.json"
	outputDir = "$buildDir/generated/openapi/esi"
	apiPackage = "com.autonomouslogic.everef.openapi.esi.api"
	modelPackage = "com.autonomouslogic.everef.openapi.esi.model"
	invokerPackage = "com.autonomouslogic.everef.openapi.esi.invoker"
	configOptions = [
			serializationLibrary: "jackson",
			dateLibrary: "java8",
			asyncNative: "false"
	]
	additionalProperties = [
			supportUrlQuery: "false"
	]
}
sourceSets.main.java.srcDirs files("${buildDir}/generated/openapi/esi/src/main/java")
		.builtBy(tasks.esiClientGenerate)
compileJava.dependsOn(tasks.esiClientGenerate)
sourcesJar.dependsOn(tasks.esiClientGenerate)
delombok.dependsOn(tasks.esiClientGenerate)

task apiSpec(type: io.swagger.v3.plugins.gradle.tasks.ResolveTask) {
	group = "openapi tools"
	filterClass = "com.autonomouslogic.customspecfilter.CustomSpecFilter"
	outputFormat = "YAML"
	prettyPrint = "true"
	sortOutput = true
	alwaysResolveAppPath = false
	skipResolveAppPath = false
	readAllResources = true
	encoding = "UTF-8"
	classpath = sourceSets.main.runtimeClasspath
	resourcePackages = ["com.autonomouslogic.everef.api"]
	outputDir = file("spec")
	outputFileName = "eve-ref-api"

	classpath = sourceSets.main.runtimeClasspath
	buildClasspath = classpath
}

task apiClientGenerate(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
	group = "openapi tools"
	generatorName = "java"
	library = "native"
	inputSpec = "$rootDir/spec/eve-ref-api.yaml"
	outputDir = "$buildDir/generated/openapi/api"
	apiPackage = "com.autonomouslogic.everef.openapi.api.api"
	modelPackage = "com.autonomouslogic.everef.openapi.api.model"
	invokerPackage = "com.autonomouslogic.everef.openapi.api.invoker"
	configOptions = [
			serializationLibrary: "jackson",
			dateLibrary: "java8",
			asyncNative: "false"
	]
	additionalProperties = [
			supportUrlQuery: "false"
	]
	schemaMappings = [
			"ActivityCost": "com.autonomouslogic.everef.model.api.ActivityCost",
			"ApiError": "com.autonomouslogic.everef.model.api.ApiError",
			"IndustryCost": "com.autonomouslogic.everef.model.api.IndustryCost",
			"IndustryCostInput": "com.autonomouslogic.everef.model.api.IndustryCostInput",
			"CopyingCost": "com.autonomouslogic.everef.model.api.CopyingCost",
			"ProductionCost": "com.autonomouslogic.everef.model.api.ProductionCost",
			"InventionCost": "com.autonomouslogic.everef.model.api.InventionCost",
			"MaterialCost": "com.autonomouslogic.everef.model.api.MaterialCost",
	]
	logToStderr = true
}
sourceSets.main.java.srcDirs files("${buildDir}/generated/openapi/api/src/main/java")
		.builtBy(tasks.apiClientGenerate)
compileJava.dependsOn(tasks.apiClientGenerate)
sourcesJar.dependsOn(tasks.apiClientGenerate)
delombok.dependsOn(tasks.apiClientGenerate)

test {
	environment "LOG_LEVEL", "trace"
	environment "DATABASE_URL", "jdbc:h2:file:${buildDir}/db/database-test;AUTO_SERVER=TRUE"
	jvmArgs "-client", "-XX:+UseG1GC", "-Xmx8g", "-Dorg.jooq.no-logo=true", "-Dorg.jooq.no-tips=true"

	useJUnitPlatform()
	testLogging {
		events = [ "FAILED", "PASSED", "SKIPPED" ]
		showExceptions = true
		exceptionFormat = "FULL"
		showExceptions = true
		showCauses = true
		showStackTraces = true
	}
	// https://junit-pioneer.org/docs/environment-variables/#warnings-for-reflective-access
	jvmArgs("--add-opens", "java.base/java.util=ALL-UNNAMED", "--add-opens", "java.base/java.lang=ALL-UNNAMED")
}

jacocoTestReport {
	executionData(test)
	reports {
		xml.required = true
		html.required = true
	}
}

spotless {
	java {
		target "src/**/*.java"
		palantirJavaFormat()
		leadingSpacesToTabs()
	}

	groovyGradle {
		leadingSpacesToTabs()
	}
}

gitSemVer {
	buildMetadataSeparator.set("-")
}

micronaut {
	version "$micronautVersion"
	runtime("netty")
	testRuntime("junit5")
}

application {
	mainClass = mainClassName
	applicationDefaultJvmArgs = [
		"-XX:+PrintCommandLineFlags",
		"-XX:MinHeapSize=" + 10 * 1024 * 1024,
		"-XX:MaxRAMPercentage=80.0",
		"-Dorg.jooq.no-logo=true",
		"-Dorg.jooq.no-tips=true"
	]
}

task placeholder(type: JavaExec) {
	group "application"
	args "placeholder"
	environment envProperties
	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task dataIndex(type: JavaExec) {
	group "application"
	args "data-index"
	environment envProperties
	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task scrapeMarketOrders(type: JavaExec) {
	group "application"
	args "scrape-market-orders"
	environment envProperties
	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task scrapePublicContracts(type: JavaExec) {
	group "application"
	args "scrape-public-contracts"
	environment envProperties
	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task buildRefData(type: JavaExec) {
	group "application"
	args "build-ref-data"
	environment envProperties
	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task publishRefData(type: JavaExec) {
	group "application"
	args "publish-ref-data"
	environment envProperties
	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task scrapeMarketHistory(type: JavaExec) {
	group "application"
	args "scrape-market-history"
	environment envProperties
	environment "ESI_RATE_LIMIT_PER_S", "5"
	environment "ESI_MARKET_HISTORY_CHUNK_SIZE", "10000"
	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task syncFuzzworkOrdersets(type: JavaExec) {
	group "application"
	args "sync-fuzzwork-ordersets"
	environment envProperties
	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task verifyRefDataModels(type: JavaExec) {
	group "application"
	args "verify-ref-data-models"
	environment envProperties
	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task scrapeHoboleaks(type: JavaExec) {
	group "application"
	args "scrape-hoboleaks"
	environment envProperties
	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task importTestResources(type: JavaExec) {
	group "application"
	args "import-test-resources"
	environment envProperties
	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task importIndustryResources(type: JavaExec) {
	group "application"
	args "import-industry-resources"
	environment envProperties
	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task flywayMigrate(type: JavaExec) {
	group "application"
	args "flyway-migrate"
	environment "DATABASE_URL", "jdbc:h2:file:${buildDir}/everef-data;AUTO_SERVER=true"
	environment envProperties

	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task importMarketHistory(type: JavaExec) {
	group "application"
	args "import-market-history"
	environment envProperties
	environment "LOG_LEVEL", "debug"
//	environment "DATABASE_URL", "jdbc:h2:file:${buildDir}/everef-data;AUTO_SERVER=true"
	environment "IMPORT_MARKET_HISTORY_MIN_DATE", LocalDate.now().minusDays(7).toString();

	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task basicLogin(type: JavaExec) {
	group "application"
	args "basic-login"
	environment envProperties

	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task scrapeStructures(type: JavaExec) {
	group "application"
	args "scrape-structures"
	environment envProperties

	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task buildSearch(type: JavaExec) {
	group "application"
	args "build-search"
	environment envProperties

	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task fetchDonations(type: JavaExec) {
	group "application"
	args "fetch-donations"
	environment envProperties

	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task syncStaticData(type: JavaExec) {
	group "application"
	args "sync-static-data"
	environment envProperties

	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task scrapeFreelanceJobs(type: JavaExec) {
	group "application"
	args "scrape-freelance-jobs"
	environment envProperties

	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task generateKeyPair(type: JavaExec) {
	group "application"
	args "generate-key-pair"
	environment envProperties

	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task api(type: JavaExec) {
	group "application"
	args "api"
	environment envProperties

	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}
