plugins {
	id "java"
	id "org.jetbrains.kotlin.jvm" version "1.8.21"
	id "idea"
	id "application"
	id "jacoco"
	id "org.danilopianini.git-sensitive-semantic-versioning-gradle-plugin" version "1.1.9"
	id "io.freefair.lombok" version "8.0.1"
	id "com.diffplug.spotless" version "6.18.0"
	id "org.openapi.generator" version "6.5.0"
}

group "com.autonomouslogic.everef"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

ext {
	mainClassName = "com.autonomouslogic.everef.Main"
	envProperties = new Properties()
	if (file("local.env").exists()) {
		file("local.env").withInputStream { envProperties.load(it) }
	}
}

repositories {
	mavenCentral()
}

dependencies {
	annotationProcessor "com.google.dagger:dagger-compiler:2.46"
	implementation "com.google.dagger:dagger:2.46"
	implementation "com.google.guava:guava:31.1-jre"
	implementation "org.apache.logging.log4j:log4j-api:2.20.0"
	implementation "org.apache.logging.log4j:log4j-core:2.20.0"
	implementation "org.apache.logging.log4j:log4j-layout-template-json:2.20.0"
	implementation "org.apache.logging.log4j:log4j-slf4j2-impl:2.20.0"
	implementation "io.reactivex.rxjava3:rxjava:3.1.6"
	implementation "com.autonomouslogic.commons:commons-java:1.6.2"
	implementation "de.neuland-bfi:pug4j:2.0.6"
	implementation "software.amazon.awssdk:s3:2.20.56"
	implementation "com.fasterxml.jackson.core:jackson-annotations:2.15.0"
	implementation "com.fasterxml.jackson.core:jackson-core:2.15.0"
	implementation "com.fasterxml.jackson.core:jackson-databind:2.15.0"
	implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.15.0"
	implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.15.0"
	implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.0"
	implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.15.0"
	implementation "org.apache.commons:commons-pool2:2.11.1"
	implementation "org.apache.commons:commons-compress:1.23.0"
	implementation "org.tukaani:xz:1.9"
	implementation "com.h2database:h2-mvstore:2.1.214"
	implementation "com.squareup.okhttp3:okhttp:4.11.0"
	implementation "org.apache.commons:commons-csv:1.10.0"
	implementation "net.gpedro.integrations.slack:slack-webhook:1.4.0"
	implementation "org.jsoup:jsoup:1.16.1"
	implementation "commons-codec:commons-codec:1.15"

	testAnnotationProcessor "com.google.dagger:dagger-compiler:2.46"
	testImplementation "com.google.dagger:dagger:2.46"
	testImplementation "org.junit.jupiter:junit-jupiter:5.9.3"
	testImplementation "org.junit.jupiter:junit-jupiter-params:5.9.3"
	testImplementation "org.junit-pioneer:junit-pioneer:2.0.1"
	testImplementation "org.mockito:mockito-junit-jupiter:5.3.1"
	testImplementation "com.squareup.okhttp3:mockwebserver:4.11.0"
}

java {
	withJavadocJar()
	withSourcesJar()
}

openApiGenerate {
	generatorName = "kotlin"
	inputSpec = "$rootDir/lib/esi-latest.json"
	packageName = "${group}.openapi.esi"
	configOptions = [
		library: "jvm-okhttp4",
		serializationLibrary: "jackson",
		dateLibrary: "java8"
	]
}
sourceSets.main.java.srcDirs += ["build/generate-resources/main/src/main/kotlin"]
compileKotlin.dependsOn(tasks.openApiGenerate)
compileJava.dependsOn(tasks.openApiGenerate)
sourcesJar.dependsOn(tasks.openApiGenerate)
delombok.dependsOn(tasks.openApiGenerate)

test {
	environment "LOG_LEVEL", "debug"
	jvmArgs "-client", "-XX:+UseG1GC", "-Xmx8g"

	useJUnitPlatform()
	testLogging {
		events = [ "FAILED", "PASSED", "SKIPPED" ]
		showExceptions = true
		exceptionFormat = "FULL"
		showExceptions = true
		showCauses = true
		showStackTraces = true
	}
	// https://junit-pioneer.org/docs/environment-variables/#warnings-for-reflective-access
	jvmArgs("--add-opens", "java.base/java.util=ALL-UNNAMED", "--add-opens", "java.base/java.lang=ALL-UNNAMED")
}

jacocoTestReport {
	executionData(test)
	reports {
		xml.required = true
		html.required = true
	}
}

spotless {
	java {
		target "src/**/*.java"
		palantirJavaFormat()
		indentWithTabs()
	}

	groovyGradle {
		indentWithTabs()
	}
}

gitSemVer {
	buildMetadataSeparator.set("-")
}

application {
	mainClass = mainClassName
	applicationDefaultJvmArgs = [
		"-XX:+PrintCommandLineFlags",
		"-XX:MinHeapSize=" + 10 * 1024 * 1024,
		"-XX:MaxRAMPercentage=80.0"
	]
}

task placeholder(type: JavaExec) {
	group "application"
	args "placeholder"
	environment envProperties
	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task dataIndex(type: JavaExec) {
	group "application"
	args "data-index"
	environment envProperties
	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task scrapeMarketOrders(type: JavaExec) {
	group "application"
	args "scrape-market-orders"
	environment envProperties
	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task scrapePublicContracts(type: JavaExec) {
	group "application"
	args "scrape-public-contracts"
	environment envProperties
	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task buildRefData(type: JavaExec) {
	group "application"
	args "build-ref-data"
	environment envProperties
	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task publishRefData(type: JavaExec) {
	group "application"
	args "publish-ref-data"
	environment envProperties
	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}

task scrapeMarketHistory(type: JavaExec) {
	group "application"
	args "scrape-market-history"
	environment envProperties
	environment "ESI_RATE_LIMIT_PER_S", "5"
	classpath = sourceSets.main.runtimeClasspath
	mainClass = mainClassName
}
